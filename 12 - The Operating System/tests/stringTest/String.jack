// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    field Array arr;
    field int length, strMaxLength;

    constructor String new(int maxLength) {
        if (maxLength < 0) {
            do Sys.error(14); // maxLength must be non-negative
        }
        if (maxLength = 0) {
            let length = 0;
            let strMaxLength = 0;
            return this; // empty string
        }
        let arr = Array.new(maxLength);
        let length = 0;
        let strMaxLength = maxLength;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        if (strMaxLength > 0) {
            do arr.dispose();
        }
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        if (j < 0 | (~(j < length))) {
            do Sys.error(15); // String index out of bounds
        }
        return arr[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        if (j < 0 | (~(j < length))) {
            do Sys.error(16); // String index out of bounds
        }
        let arr[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length = strMaxLength) {
            do Sys.error(17); // string is full
        }
        let arr[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length = 0) {
            do Sys.error(18); // string is empty
        }
        let length = length - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        // Pseudocode:
        // 1. let val = 0
        // 2. for (i = 0 .. str.length()) do
        // 3.   let d = integer value of str.charAt(i)
        // 4.   let val = (val*10) + d
        // 5. return val
        var int i, val, d, sign;
        let val = 0;
        let i = 0;
        let sign = 1; // positive number by default
        if (charAt(0) = 45) { // ascii code of '-' is 45
            // negative number
            let i = 1;
            let sign = -1;

        }
        while (i < length) {
            let d = charAt(i) - 48; // ascii code of '0' is 48
            if (d < 0 | (~(d < 10))) {
               // non-digit character is detected - then break
                return Math.multiply(val, sign);
            }
            let val = Math.multiply(val, 10) + d;
            let i = i + 1;
        }
        return Math.multiply(val, sign);
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        // Pseudocode:
        // 1. let lastDigit = val % 10
        // 2. let c = character representing lastDigit
        // 3. if (val < 10)
        // 4.   return c (as a string)
        // 5. else
        // 6.   return int2String(val / 10).appendChar(c)

        var int lastDigit, positiveVar;
        let length = 0;
        // extract digits from a non-negative variable
        let positiveVar = val;
        //do Output.printInt(val); // for debugging
        //do Output.println();   // for debugging
        if (val < 0) {
            // append sign first - if negative
            if (length + 1 > strMaxLength) {
                //do Output.printString(this);
                do Sys.error(19); // Insufficient space in string
            }
            do appendChar(45); // '-' ascii code
            let positiveVar = -val; // work with positive variable for next extractions
        }

        do setIntRecursive(positiveVar);
        
        return;
    }

    method void setIntRecursive(int positiveVar) {
        var int lastDigit;

        // lastDigit = positive % 10
        let lastDigit = positiveVar - Math.multiply(Math.divide(positiveVar, 10), 10);

        if (positiveVar < 10) {
            // single digit remain in the variable
            if (length + 1 > strMaxLength) {
                do Sys.error(19); // Insufficient space in string
            }
            do appendChar(lastDigit + 48);
        } else {
            // extract digits recursively
            do setIntRecursive(Math.divide(positiveVar, 10));
            if (length + 1 > strMaxLength) {
                do Sys.error(19); // Insufficient space in string
            }
            do appendChar(lastDigit + 48); // '0' ascii code is 48
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128; //ascii code of '\n'
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129; //ascii code of '\b'
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34; //ascii code of '"'
    }
}
