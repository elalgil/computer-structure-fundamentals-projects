// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    
        static boolean color; 
        static Array powerOf2; // holds the powers of 2, from 2^0 to 2^15

        /** Initializes the Screen. */
        function void init() {
            do Screen.setColor(true); // black
            let powerOf2 = Array.new(16); // array holds 16 bits of a number
            let powerOf2[0] = 1;
            let powerOf2[1] = 2;
            let powerOf2[2] = 4;
            let powerOf2[3] = 8;
            let powerOf2[4] = 16;
            let powerOf2[5] = 32;
            let powerOf2[6] = 64;
            let powerOf2[7] = 128;
            let powerOf2[8] = 256;
            let powerOf2[9] = 512;
            let powerOf2[10] = 1024;
            let powerOf2[11] = 2048;
            let powerOf2[12] = 4096;
            let powerOf2[13] = 8192;
            let powerOf2[14] = 16384;
            let powerOf2[15] = 16384 + 16384;

            do Screen.clearScreen();
            return;
        }

        /** Erases the entire screen. */
        function void clearScreen() {
            var int i;
            let i = 0;

            while (i < 8192) { // 256*512/16 = 8192
                do Memory.poke(16384 + i, 0); // clear the screen
                let i = i + 1;
            }
            
            //do Screen.setColor(false); // white
            //do Screen.drawRectangle(0, 0, 511, 255); // fill the entire screen with white
            //do Screen.setColor(true); // black
            return;
        }

        /** Sets the current color, to be used for all subsequent drawXXX commands.
         *    Black is represented by true, white by false. */
        function void setColor(boolean b) {
            let color = b;
            return;
        }

        /** Draws the (x,y) pixel, using the current color. */
        function void drawPixel(int x, int y) {
            // For this function, you will need to calculate the value x%16.
            // It should be calculated quickly without using multiplication or
            // division, using exactly one basic math/logical operation.
            // In addition, calculating 16384 + y * 32 + x/16 should not use
            // division or multiplication.
            // Pseudocode:
            // 1. Compute the RAM address where the (x,y) pixel is
            //    represented: 16384 + (32*y) + (x/16).
            // 2. Use Memory.peek to get the 16-bit value of this address
            // 3. Use some bitwise operation to set (only) the bit that corresponds
            //    to the pixel to the current color.
            // 4. Use Memory.poke to write the modified 16-bit value to the RAM
            // address.
            var int address, word, mask, remainder;
            if ((x < 0) | (x > 511) | (y < 0) | (y > 255)) {
                do Sys.error(7); // Illegal pixel coordinates
            }
            let remainder = x & 15; // x % 16
            let mask = powerOf2[remainder];  // equivalent to: 1 << (15 - remainder)
            let address = 16384 + Math.multiply(32, y) + Math.divide(x, 16);
            let word = Memory.peek(address);

            // decide set or clear using 'color' (assume 1 = set, 0 = clear)
            if (color) {
                // set the bit to 1 == black
                let word = word | mask;
            } else {
                // clear the bit == 0
                let word = word & (~mask);
            }
            do Memory.poke(address, word);
            return;
        }


        /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
        function void drawLine(int x1, int y1, int x2, int y2) {
            // The case where x1 != x2 and y1 != y2 should be implemented without
            // multiplication or division.
            // Pseudocode:
            // 1. let x = x1, y = y1, a = 0, b = 0, diff = 0
            // 2. Compute dx and dy
            // 3. while ((a <= dx) and (b <= dy))
            // 4.   do drawPixel(x+a, y+b)
            // 5.   if (diff < 0) { let a=a+1, diff=diff+dy }
            // 6.   else          { let b=b+1, diff=diff-dx }
            //
            // The case where y1 == y2 can be implemented efficiently by
            // trying to draw as many pixels as possible in a single assignment,
            // similarly to Fill in project 4.

            var int x, y;
            var int dx, dy, a, b, diff, tmp;
            if (x1 < 0 | (x1 > 511) | (x2 < 0) | (x2 > 511) | (y1 < 0) | (y1 > 255) | (y2 < 0) | (y2 > 255)) {
                do Sys.error(8); // Illegal line coordinates
            }
            // normalize points so that x1 <= x2
            // this makes the algorithm only deal with "rightward" lines
            if (x1 > x2) {
                // swap x1 and x2
                let tmp = x1;
                let x1 = x2;
                let x2 = tmp;

                // swap y1 and y2 to keep the same geometric segment
                let tmp = y1;
                let y1 = y2;
                let y2 = tmp;
            }
            
            if (y1 = y2) { // horizontal line
                let x = x1;
                while (~(x > x2)) {
                    do Screen.drawPixel(x, y1);
                    let x = x + 1;
                }
                return;
            }  
            
            if (x1 = x2) { // vertical line
                    if (y1 > y2) { // ensure y1 <= y2
                        let tmp = y1;
                        let y1 = y2;
                        let y2 = tmp;
                    }
                    let y = y1;
                    
                    while (~(y > y2)) {
                        do Screen.drawPixel(x1, y);
                        let y = y + 1;
                    }
                    return;
            } else {
                // diagonal line
                let diff = 0;
                let a = 0;
                let b = 0;
                let dx = x2 - x1;
                let dy = y2 - y1;

                if (y1 < y2) {
                    // the line goes south east inc x, inc y
                    while (~(a > dx) & ~(b > dy)) {
                        do Screen.drawPixel(x1 + a, y1 + b);
                        if (diff < 0) {
                            let a = a + 1;
                            let diff = diff + dy;
                        } else {
                            let b = b + 1;
                            let diff = diff - dx;
                        }
                    }
                } else {
                    /* the line goes north east inc x, dec y
                    do Output.printInt(x1); // for debugging
                    do Output.printChar(44); // for debugging
                    do Output.printInt(y1); // for debugging
                    do Output.println();   // for debugging
                    do Output.printInt(x2); // for debugging
                    do Output.printChar(44); // for debugging
                    do Output.printInt(y2); // for debugging
                    do Output.println();   // for debugging
                    do Output.printInt(dx); // for debugging
                    do Output.printChar(44); // for debugging
                    do Output.printInt(dy); // for debugging
                    do Output.println();   // for debugging
                    */
                    let dy = -dy;
                    while(~(a > dx) & ~(b > dy)) {
                        do Screen.drawPixel(x1 + a, y1 - b);
                        if(diff < 0){
                            let a = a + 1;
                            let diff = diff + dy;
                        } else {
                            let b = b + 1;
                            let diff = diff - dx;
                        }
                    }
                }
            }
            return;
        }

        /** Draws a filled rectangle whose top left corner is (x1, y1)
         * and bottom right corner is (x2,y2), using the current color. */
        function void drawRectangle(int x1, int y1, int x2, int y2) {
            var int height, width;
            var int x3, y3, x4, y4;

            let height = y2 - y1;
            let width = x2 - x1;
            let x3 = x1 + width;
            let y3 = y1;
            let x4 = x1 + width;
            let y4 = y1 + height;
            if (x1 < 0 | (x1 > 511) | (x2 < 0) | (x2 > 511) | (y1 < 0) | (y1 > 255) | (y2 < 0) | (y2 > 255)) {
                do Sys.error(9); // Illegal rectangle coordinates
            }
            if ((width < 0) | (height < 0)) {
                do Sys.error(9); // Illegal rectangle coordinates
            }
            do Screen.drawLine(x1, y1, x3, y3); // top side
            while (y1 < y2) {
                let y1 = y1 + 1;
                do Screen.drawLine(x1, y1, x3, y1); // fill the rectangle
            }
            return;
        }

        /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
        function void drawCircle(int x, int y, int r) {
            // This can be implemented efficiently by drawing multiple lines at
            // each iteration. You can use multiplication and sqrt.
            // Pseudocode:
            // 1. for (dy = -r ... r)
            // 2.   let halfWidth = sqrt(r*r - dy*dy)
            // 3.   do drawLine(x-halfWidth, y+dy, x+halfWidth, y+dy)
            var int dy, x1, x2, rSquared, halfWidth;
            if (x < 0 | (x > 511) | (y < 0) | (y > 255)) {
                do Sys.error(12); // Illegal center coordinates
            }
            if (r < 0 | (r > 181)) {
                do Sys.error(13); // Illegal radius
            }
            let rSquared = Math.multiply(r, r);
            let dy = -r;
            while (~(dy > r)) {
                let halfWidth = Math.sqrt(rSquared - Math.multiply(dy, dy));
                let x1 = x - halfWidth;
                let x2 = x + halfWidth;
                do Screen.drawLine(x1, y + dy, x2, y + dy);
                let dy = dy + 1;
            }
            return;
        }

        /** Disposes the Screen. */
        function void dispose() {
            do Memory.deAlloc(powerOf2);
            return;
        }
}
