// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
        static Array arr;
        
        /** Initializes the library. */
        function void init() {
        
            let arr = Array.new(16); // array holds 16 bits of a number
            let arr[0] = 1;
            let arr[1] = 2;
            let arr[2] = 4;
            let arr[3] = 8;
            let arr[4] = 16;
            let arr[5] = 32;
            let arr[6] = 64;
            let arr[7] = 128;
            let arr[8] = 256;
            let arr[9] = 512;
            let arr[10] = 1024;
            let arr[11] = 2048;
            let arr[12] = 4096;
            let arr[13] = 8192;
            let arr[14] = 16384;
            let arr[15] = 16384 + 16384; // 32768
            return;
        }

        /** Disposes the Math library. */
        function void dispose() {
            do arr.dispose();
            return;
        }

        /** Returns the absolute value of x. */
        function int abs(int x) {
            if (x < 0) {
                return -x;
            } else {
                return x;
            }
        }

        /** 
         * Returns the product of x and y. 
         * When a Jack compiler detects the multiplication operator '*' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x*y and multiply(x,y) return the same value.
         */
        function int multiply(int x, int y) {
            // Pseudocode:
            // 1. let sum = 0
            // 2. let shiftedx = x
            // 3. for i = 0 ... n-1 do
            // 4.   if ((i-th bit of y) == 1)
            // 5.      let sum = sum + shiftedx
            // 6.   let shiftedx = 2*shiftedx
            // 7. return sum
            var int i, sum, shiftedx, bit;
            let sum = 0;
            let shiftedx = x;
            let i = 0;
            while (i < 16) {
                let bit = Math.bit(y, i);
                if (~(bit = 0)) { // if (i-th bit of y) == 1
                    let sum = sum + shiftedx;
                }
                let shiftedx = shiftedx + shiftedx; // 2*shiftedx
                let i = i + 1;
            }
            return sum;
        }

        /** 
         * Returns the integer part of x/y.
         * When a Jack compiler detects the multiplication operator '/' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x/y and divide(x,y) return the same value.
         */
        function int divide(int x, int y) {
            // This should be implemented without using multiplication.
            // Hint: updating the value of 2*q*y in the "if" statement requires
            // at most a single addition operator at every recursion step.
            // Pseudocode:
            // 1. if (y > x) return 0
            // 2. let q = divide(x, 2*y)
            // 3. if ((x - 2*q*y) < y)
            // 4.   return 2*q
            // 5. else
            // 6.   return 2*q + 1
            var int q, result;
            var boolean y_sign, x_sign;

            if (y = 0) {
                do Sys.error(3); // division by zero
            }
            let y_sign = y < 0;
            let x_sign = x < 0;
            let y = Math.abs(y);
            let x = Math.abs(x);

            if (y + y < 0)
            {
                return 0; // overflow
            }
            if (y > x) {
                return 0;
            }

            let q = Math.divide(x, y + y);
            if ((x - Math.multiply(q, y + y)) < y) { // 2*q*y = q*(y+y)
                let result = q + q;
            } else {
                let result = (q + q + 1);
            }
            if (~(x_sign = y_sign)) { // if signs are different
                return -result;
            } else {
                return result;
            }
        }

        /** Returns the integer part of the square root of x. */
        function int sqrt(int x) {
            // This should be implemented without using multiplication or division.
            // Pseudocode (where '**' is the mathematical power function):
            // 1. let y = 0
            // 2. for j = (n/2 - 1) ... 0 do
            // 3.   if ((y + 2**j)**2 <= x) then let y = y + 2**j
            // 4. return y
            var int y, j, calc;

            if (x < 0) {
                do Sys.error(4); // cannot compute square root of a negative number
            }

            let j = 7; // j = (n/2 - 1), n=16
            let y = 0;
            while (~(j < 0)) {
                let calc = Math.multiply((y + arr[j]), (y + arr[j])); // 2**j = 1^j = arr[j]
                if (~(calc > x) & (calc > 0)) {
                    let y = y + arr[j];
                }
                let j = j - 1;
            }
            return y;
        }

        /** Returns the greater number. */
        function int max(int a, int b) {
            if (a > b) {
                return a;
            } else {
                return b;
            }
        }

        /** Returns the smaller number. */
        function int min(int a, int b) {
            if (a < b) {
                return a;
            } else {
                return b;
            }
        }

        /** function return the i-th bit of x (0 <= i < 16). */
        function int bit(int x, int i) {
            return x & arr[i];
        }
}
