// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
	
	Mux16(a=instruction ,b=outALU ,sel=instruction[15] ,out=chooseA);
	
	Mux(a=true, b=instruction[5], sel=instruction[15], out=openA);
	ARegister(in=chooseA ,load=openA ,out=outA, out[0..14]=addressM);
	
	And(a=instruction[4], b=instruction[15], out=openD);
	DRegister(in=outALU ,load=openD ,out=outD);
	
	Mux16(a=outA ,b=inM ,sel=instruction[12] ,out=AorM);
	
	ALU(x=outD ,y=AorM ,zx=instruction[11] ,nx=instruction[10] ,zy=instruction[9] ,ny=instruction[8] ,f=instruction[7] ,no=instruction[6] ,out=outALU, out=outM ,zr=outZr ,ng=outNg);
	
	//j3 = instruction[0]
	//j2 = instruction[1]
	//j1 = instruction[2]
	//gt = ~(zr) & ~(ng)
	//shouldJump = (j3 & gt) | (j2 & zr) | (j1 & ng)
	
	Not(in=outZr, out=nZr);
	Not(in=outNg, out=nNg);
	And(a=nZr, b=nNg, out=gtRes);
	
	And(a=outNg, b=instruction[2], out=res3);
	And(a=outZr, b=instruction[1], out=res2);
	And(a=gtRes, b=instruction[0], out=res1);
	
	Or(a=res1 ,b=res2 ,out=or1);
	Or(a=or1 ,b=res3 ,out=conditionBit);
	
	Or(a=instruction[15], b=false, out=Ccommand);
    And(a=Ccommand, b=conditionBit, out=pcLoad);
	
	PC(in=outA ,load=pcLoad ,inc=true ,reset=reset ,out[0..14]=pc);
	And(a=instruction[3], b=instruction[15], out=writeM);
	
}